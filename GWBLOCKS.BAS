10 DEFINT A-Z: GOSUB 950: END
20 FULLROW = 0
30 FOR PITY = 0 TO 15
40 FULLROW = -1
50 FOR PITX = 0 TO 9
60 IF MID$(PIT$, ((10 * PITY) + PITX) + 1, 1) = "0" THEN FULLROW = 0: GOTO 80
70 NEXT PITX
80 IF FULLROW THEN REMOVEDROW = PITY: GOSUB 1150
90 NEXT PITY
100 RETURN
110 DROPRATE! = 1
120 SHAPE = INT(RND * 6)
130 SHAPEANGLE = INT(RND * 4)
140 NEWANGLE = SHAPEANGLE
150 GOSUB 560
160 SHAPEMAP$ = ROTATEDMAP$
170 GOSUB 500
180 SHAPEX = RANDOMSHAPEX
190 SHAPEY = -4
200 RETURN
210 COLOR 4: LOCATE 3, 2
220 IF GAMEOVER THEN PRINT "Game over.": LOCATE , 2: PRINT "Press Enter to play a new game.";  ELSE PRINT ; "Score:" + STR$(SCORE);
230 RETURN
240 LINE (116, 32)-STEP(92, 145), 15, B
250 LINE (116, 32)-STEP(92, 0), 0
260 FOR PITY = 0 TO 15
270 FOR PITX = 0 TO 9
280 IF GAMEOVER THEN BLOCKCOLOR$ = "4" ELSE BLOCKCOLOR$ = MID$(PIT$, ((10 * PITY) + PITX) + 1, 1)
290 LINE ((PITX * 9) + 117, (PITY * 9) + 32)-STEP(9, 9), VAL("&H" + BLOCKCOLOR$), BF
300 LINE ((PITX * 9) + 118, (PITY * 9) + 33)-STEP(6, 6), 0, B
310 NEXT PITX: NEXT PITY
320 RETURN
330 FOR BLOCKX = 0 TO 3
340 FOR BLOCKY = 0 TO 3
350 PITX = SHAPEX + BLOCKX: PITY = SHAPEY + BLOCKY
360 IF PITX >= 0 AND PITX < 10 AND PITY >= 0 AND PITY < 16 THEN GOSUB 390
370 NEXT BLOCKY: NEXT BLOCKX
380 RETURN
390 BLOCKCOLOR$ = MID$(SHAPEMAP$, ((4 * BLOCKY) + BLOCKX) + 1, 1)
400 IF BLOCKCOLOR$ = "0" THEN BLOCKCOLOR$ = MID$(PIT$, ((10 * PITY) + PITX) + 1, 1)
410 LINE ((PITX * 9) + 117, (PITY * 9) + 32)-STEP(9, 9), VAL("&H" + BLOCKCOLOR$), BF
420 LINE ((PITX * 9) + 118, (PITY * 9) + 33)-STEP(6, 6), 0, B
430 RETURN
440 FOR BLOCKX = 0 TO 3
450 FOR BLOCKY = 0 TO 3
460 PITX = SHAPEX + BLOCKX: PITY = SHAPEY + BLOCKY
470 IF PITX >= 0 AND PITX < 10 AND PITY >= 0 AND PITY < 16 THEN LINE ((PITX * 9) + 117, (PITY * 9) + 32)-STEP(9, 9), VAL("&H" + MID$(PIT$, ((10 * PITY) + PITX) + 1, 1)), BF
480 NEXT BLOCKY: NEXT BLOCKX
490 RETURN
500 INTENDEDSHAPEX = INT((RND * 10) - 1): RANDOMSHAPEX = 0
510 FOR XMOVE = 0 TO INTENDEDSHAPEX
520 MOVEDMAP$ = SHAPEMAP$: XDIRECTION = 1: YDIRECTION = 0: GOSUB 670
530 IF CANMOVE THEN RANDOMSHAPEX = RANDOMSHAPEX + 1 ELSE RETURN
540 NEXT XMOVE
550 RETURN
560 GOSUB 770
570 IF NEWANGLE = 0 THEN ROTATEDMAP$ = MAP$: RETURN
580 ROTATEDMAP$ = STRING$(4 * 4, "0")
590 FOR BLOCKX = 0 TO 3
600 FOR BLOCKY = 0 TO 3
610 IF NEWANGLE = 1 THEN NEWBLOCKX = 3 - BLOCKY: NEWBLOCKY = BLOCKX
620 IF NEWANGLE = 2 THEN NEWBLOCKX = 3 - BLOCKX: NEWBLOCKY = 3 - BLOCKY
630 IF NEWANGLE = 3 THEN NEWBLOCKX = BLOCKY: NEWBLOCKY = 3 - BLOCKX
640 MID$(ROTATEDMAP$, ((4 * NEWBLOCKY) + NEWBLOCKX) + 1, 1) = MID$(MAP$, ((4 * BLOCKY) + BLOCKX) + 1, 1)
650 NEXT BLOCKY: NEXT BLOCKX
660 RETURN
670 CANMOVE = -1
680 FOR BLOCKY = 0 TO 3
690 FOR BLOCKX = 0 TO 3
700 IF NOT MID$(MOVEDMAP$, ((4 * BLOCKY) + BLOCKX) + 1, 1) = "0" THEN GOSUB 730
710 NEXT BLOCKX: NEXT BLOCKY
720 RETURN
730 PITX = (SHAPEX + BLOCKX) + XDIRECTION: PITY = (SHAPEY + BLOCKY) + YDIRECTION
740 IF PITX >= 0 AND PITX < 10 AND PITY >= 0 AND PITY < 16 THEN IF NOT MID$(PIT$, (((10 * PITY) + PITX) + 1), 1) = "0" THEN CANMOVE = 0: RETURN
750 IF PITX < 0 OR PITX >= 10 OR PITY >= 16 THEN CANMOVE = 0: RETURN
760 RETURN
770 MAP$ = ""
780 IF SHAPE = 0 THEN MAP$ = "0000333300000000"
790 IF SHAPE = 1 THEN MAP$ = "0000111000100000"
800 IF SHAPE = 2 THEN MAP$ = "0000666060000000"
810 IF SHAPE = 3 THEN MAP$ = "00000EE00EE00000"
820 IF SHAPE = 4 THEN MAP$ = "0000022022000000"
830 IF SHAPE = 5 THEN MAP$ = "0000555005000000"
840 IF SHAPE = 7 THEN MAP$ = "0000440004400000"
850 RETURN
860 RANDOMIZE TIMER: KEY OFF: PLAY "ML L64": SCREEN 7: CLS : COLOR 9
870 LOCATE 1, 1: PRINT "GWBlocks v1.00": PRINT "By: Peter Swinkels, ***2021***";
880 GOSUB 110
890 GAMEOVER = 0
900 PIT$ = STRING$(10 * 16, "0")
910 SCORE = 0
920 GOSUB 240
930 GOSUB 210
940 RETURN
950 GOSUB 860
960 STARTTIME! = TIMER
970 WHILE NOT KEYV$ = CHR$(27)
980 KEYV$ = ""
990 WHILE KEYV$ = ""
1000 IF (NOT GAMEOVER) AND (TIMER >= STARTTIME! + DROPRATE! OR STARTTIME! > TIMER) THEN GOSUB 1340: STARTTIME! = TIMER
1010 KEYV$ = INKEY$
1020 WEND
1030 GOSUB 440
1040 IF KEYV$ = CHR$(27) THEN SCREEN 0: WIDTH 80: KEY ON: END
1050 IF GAMEOVER AND KEYV$ = CHR$(13) THEN GOSUB 860
1060 IF (NOT GAMEOVER) AND (KEYV$ = "A" OR KEYV$ = "a") THEN GOSUB 1220
1070 IF (NOT GAMEOVER) AND (KEYV$ = CHR$(0) + "K") THEN MOVEDMAP$ = SHAPEMAP$: XDIRECTION = -1: YDIRECTION = 0: GOSUB 670
1080 IF (NOT GAMEOVER) AND (KEYV$ = CHR$(0) + "K" AND CANMOVE) THEN SHAPEX = SHAPEX - 1
1090 IF (NOT GAMEOVER) AND (KEYV$ = CHR$(0) + "M") THEN MOVEDMAP$ = SHAPEMAP$: XDIRECTION = 1: YDIRECTION = 0: GOSUB 670
1100 IF (NOT GAMEOVER) AND (KEYV$ = CHR$(0) + "M" AND CANMOVE) THEN SHAPEX = SHAPEX + 1
1110 IF (NOT GAMEOVER) AND (KEYV$ = " ") THEN DROPRATE! = 0
1120 GOSUB 330
1130 WEND
1140 RETURN
1150 FOR PITROWY = REMOVEDROW TO 0 STEP -1
1160 FOR PITX = 0 TO 10 - 1
1170 IF PITROWY = 0 THEN BLOCKCOLOR$ = "0" ELSE BLOCKCOLOR$ = MID$(PIT$, ((10 * (PITROWY - 1)) + PITX) + 1, 1)
1180 MID$(PIT$, ((10 * PITROWY) + PITX) + 1, 1) = BLOCKCOLOR$
1190 NEXT PITX: NEXT PITROWY
1200 SCORE = SCORE + 1
1210 RETURN
1220 IF SHAPEANGLE = 3 THEN NEWANGLE = 0 ELSE NEWANGLE = SHAPEANGLE + 1
1230 GOSUB 560
1240 MOVEDMAP$ = ROTATEDMAP$: XDIRECTION = 0: YDIRECTION = 0: GOSUB 670
1250 IF CANMOVE THEN SHAPEANGLE = NEWANGLE: SHAPEMAP$ = ROTATEDMAP$
1260 RETURN
1270 PLAY "N21"
1280 FOR BLOCKY = 0 TO 3
1290 FOR BLOCKX = 0 TO 3
1300 PITX = SHAPEX + BLOCKX: PITY = SHAPEY + BLOCKY
1310 IF (PITX >= 0 AND PITX < 10 AND PITY >= 0 AND PITY < 16) AND (NOT MID$(SHAPEMAP$, ((4 * BLOCKY) + BLOCKX) + 1, 1) = "0") THEN MID$(PIT$, ((10 * PITY) + PITX) + 1, 1) = MID$(SHAPEMAP$, ((4 * BLOCKY) + BLOCKX) + 1, 1)
1320 NEXT BLOCKX: NEXT BLOCKY
1330 RETURN
1340 MOVEDMAP$ = SHAPEMAP$: XDIRECTION = 0: YDIRECTION = 1: GOSUB 670
1350 IF CANMOVE THEN GOSUB 1370 ELSE GOSUB 1410
1360 RETURN
1370 GOSUB 440
1380 IF DROPRATE! = 1 THEN SOUND 37, .3
1390 SHAPEY = SHAPEY + 1: GOSUB 330
1400 RETURN
1410 GOSUB 1270
1420 GAMEOVER = (SHAPEY < 0)
1430 GOSUB 20
1440 GOSUB 240
1450 GOSUB 210
1460 IF NOT GAMEOVER THEN GOSUB 110: GOSUB 330
1470 RETURN

