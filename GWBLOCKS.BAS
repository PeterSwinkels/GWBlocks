10 DEFINT A-Z: GOSUB 730: END
20 FULLROW = 0
30 FOR PITY = 0 TO 15
40 FULLROW = -1
50 FOR PITX = 0 TO 9
60 IF MID$(PIT$, ((10 * PITY) + PITX) + 1, 1) = "0" THEN FULLROW = 0: GOTO 80
70 NEXT PITX
80 IF FULLROW THEN REMOVEDROW = PITY: GOSUB 930
90 NEXT PITY
100 RETURN
110 DROPRATE! = 1
120 SHAPE = INT(RND * 6)
130 SHAPEANGLE = INT(RND * 4)
140 NEWANGLE = SHAPEANGLE
150 GOSUB 410
160 SHAPEMAP$ = ROTATEDMAP$
170 GOSUB 350
180 SHAPEX = RANDOMSHAPEX
190 SHAPEY = -4
200 RETURN
210 COLOR 4: LOCATE 3, 2
220 IF GAMEOVER THEN PRINT "Game over.": LOCATE , 2: PRINT "Press Enter to play a new game.";  ELSE PRINT ; "Score:" + STR$(SCORE);
230 RETURN
240 LINE (116, 32)-STEP(92, 145), 15, B
250 LINE (116, 32)-STEP(92, 0), 0
260 FOR PITY = 0 TO 15
270 Y = PITY * 9
280 FOR PITX = 0 TO 9
290 IF GAMEOVER THEN BLOCKCOLOR$ = "4" ELSE BLOCKCOLOR$ = MID$(PIT$, ((10 * PITY) + PITX) + 1, 1)
300 X = PITX * 9
310 LINE (X + 117, Y + 32)-STEP(9, 9), VAL("&H" + BLOCKCOLOR$), BF
320 LINE (X + 118, Y + 33)-STEP(6, 6), 0, B
330 NEXT PITX: NEXT PITY
340 RETURN
350 INTENDEDSHAPEX = INT((RND * 10) - 1): RANDOMSHAPEX = 0
360 FOR XMOVE = 0 TO INTENDEDSHAPEX
370 MOVEDMAP$ = SHAPEMAP$: XDIRECTION = 1: YDIRECTION = 0: GOSUB 510
380 IF CANMOVE THEN RANDOMSHAPEX = RANDOMSHAPEX + 1 ELSE RETURN
390 NEXT XMOVE
400 RETURN
410 IF NEWANGLE = 0 THEN ROTATEDMAP$ = MAPS$(SHAPE): RETURN
420 ROTATEDMAP$ = STRING$(4 * 4, "0")
430 FOR BLOCKX = 0 TO 3
440 FOR BLOCKY = 0 TO 3
450 IF NEWANGLE = 1 THEN NEWBLOCKX = 3 - BLOCKY: NEWBLOCKY = BLOCKX
460 IF NEWANGLE = 2 THEN NEWBLOCKX = 3 - BLOCKX: NEWBLOCKY = 3 - BLOCKY
470 IF NEWANGLE = 3 THEN NEWBLOCKX = BLOCKY: NEWBLOCKY = 3 - BLOCKX
480 MID$(ROTATEDMAP$, ((4 * NEWBLOCKY) + NEWBLOCKX) + 1, 1) = MID$(MAPS$(SHAPE), ((4 * BLOCKY) + BLOCKX) + 1, 1)
490 NEXT BLOCKY: NEXT BLOCKX
500 RETURN
510 CANMOVE = -1
520 FOR BLOCKY = 0 TO 3
530 Y = 4 * BLOCKY
540 FOR BLOCKX = 0 TO 3
550 IF NOT MID$(MOVEDMAP$, (Y + BLOCKX) + 1, 1) = "0" THEN GOSUB 580
560 NEXT BLOCKX: NEXT BLOCKY
570 RETURN
580 PITX = (SHAPEX + BLOCKX) + XDIRECTION: PITY = (SHAPEY + BLOCKY) + YDIRECTION
590 IF PITX >= 0 AND PITX < 10 AND PITY >= 0 AND PITY < 16 THEN IF NOT MID$(PIT$, (((10 * PITY) + PITX) + 1), 1) = "0" THEN CANMOVE = 0: RETURN
600 IF PITX < 0 OR PITX >= 10 OR PITY >= 16 THEN CANMOVE = 0: RETURN
610 RETURN
620 RETURN
630 RANDOMIZE TIMER: KEY OFF: PLAY "ML L64": SCREEN 7: CLS : COLOR 9
640 MAPS$(0) = "0000333300000000": MAPS$(1) = "0000111000100000": MAPS$(2) = "0000666060000000": MAPS$(3) = "00000EE00EE00000": MAPS$(4) = "0000022022000000": MAPS$(5) = "0000555005000000": MAPS$(6) = "0000440004400000"
650 LOCATE 1, 1: PRINT "GWBlocks v1.01": PRINT "By: Peter Swinkels, ***2021***";
660 GOSUB 110
670 GAMEOVER = 0
680 PIT$ = STRING$(10 * 16, "0")
690 SCORE = 0
700 GOSUB 240
710 GOSUB 210
720 RETURN
730 GOSUB 630
740 STARTTIME! = TIMER
750 WHILE NOT KEYV$ = CHR$(27)
760 KEYV$ = ""
770 WHILE KEYV$ = ""
780 IF (NOT GAMEOVER) AND (TIMER >= STARTTIME! + DROPRATE! OR STARTTIME! > TIMER) THEN GOSUB 1130: STARTTIME! = TIMER
790 KEYV$ = INKEY$
800 WEND
810 GOSUB 1380
820 IF KEYV$ = CHR$(27) THEN SCREEN 0: WIDTH 80: KEY ON: END
830 IF GAMEOVER AND KEYV$ = CHR$(13) THEN GOSUB 630
840 IF (NOT GAMEOVER) AND (KEYV$ = "A" OR KEYV$ = "a") THEN GOSUB 1000
850 IF (NOT GAMEOVER) AND (KEYV$ = CHR$(0) + "K") THEN MOVEDMAP$ = SHAPEMAP$: XDIRECTION = -1: YDIRECTION = 0: GOSUB 510
860 IF (NOT GAMEOVER) AND (KEYV$ = CHR$(0) + "K" AND CANMOVE) THEN SHAPEX = SHAPEX - 1
870 IF (NOT GAMEOVER) AND (KEYV$ = CHR$(0) + "M") THEN MOVEDMAP$ = SHAPEMAP$: XDIRECTION = 1: YDIRECTION = 0: GOSUB 510
880 IF (NOT GAMEOVER) AND (KEYV$ = CHR$(0) + "M" AND CANMOVE) THEN SHAPEX = SHAPEX + 1
890 IF (NOT GAMEOVER) AND (KEYV$ = " ") THEN DROPRATE! = 0
900 GOSUB 1270
910 WEND
920 RETURN
930 FOR PITROWY = REMOVEDROW TO 0 STEP -1
940 FOR PITX = 0 TO 10 - 1
950 IF PITROWY = 0 THEN BLOCKCOLOR$ = "0" ELSE BLOCKCOLOR$ = MID$(PIT$, ((10 * (PITROWY - 1)) + PITX) + 1, 1)
960 MID$(PIT$, ((10 * PITROWY) + PITX) + 1, 1) = BLOCKCOLOR$
970 NEXT PITX: NEXT PITROWY
980 SCORE = SCORE + 1
990 RETURN
1000 IF SHAPEANGLE = 3 THEN NEWANGLE = 0 ELSE NEWANGLE = SHAPEANGLE + 1
1010 GOSUB 410
1020 MOVEDMAP$ = ROTATEDMAP$: XDIRECTION = 0: YDIRECTION = 0: GOSUB 510
1030 IF CANMOVE THEN SHAPEANGLE = NEWANGLE: SHAPEMAP$ = ROTATEDMAP$
1040 RETURN
1050 PLAY "N21"
1060 FOR BLOCKY = 0 TO 3
1070 Y = 4 * BLOCKY
1080 FOR BLOCKX = 0 TO 3
1090 PITX = SHAPEX + BLOCKX: PITY = SHAPEY + BLOCKY
1100 IF (PITX >= 0 AND PITX < 10 AND PITY >= 0 AND PITY < 16) AND (NOT MID$(SHAPEMAP$, (Y + BLOCKX) + 1, 1) = "0") THEN MID$(PIT$, ((10 * PITY) + PITX) + 1, 1) = MID$(SHAPEMAP$, ((4 * BLOCKY) + BLOCKX) + 1, 1)
1110 NEXT BLOCKX: NEXT BLOCKY
1120 RETURN
1130 MOVEDMAP$ = SHAPEMAP$: XDIRECTION = 0: YDIRECTION = 1: GOSUB 510
1140 IF CANMOVE THEN GOSUB 1160 ELSE GOSUB 1200
1150 RETURN
1160 GOSUB 1380
1170 IF DROPRATE! = 1 THEN SOUND 37, .3
1180 SHAPEY = SHAPEY + 1: GOSUB 1270
1190 RETURN
1200 GOSUB 1050
1210 GAMEOVER = (SHAPEY < 0)
1220 GOSUB 20
1230 GOSUB 240
1240 GOSUB 210
1250 IF NOT GAMEOVER THEN GOSUB 110: GOSUB 1270
1260 RETURN
1270 FOR BLOCKX = 0 TO 3
1280 FOR BLOCKY = 0 TO 3
1290 PITX = SHAPEX + BLOCKX: PITY = SHAPEY + BLOCKY
1300 IF PITX >= 0 AND PITX < 10 AND PITY >= 0 AND PITY < 16 THEN GOSUB 1330
1310 NEXT BLOCKY: NEXT BLOCKX
1320 RETURN
1330 BLOCKCOLOR$ = MID$(SHAPEMAP$, ((4 * BLOCKY) + BLOCKX) + 1, 1)
1340 IF BLOCKCOLOR$ = "0" THEN BLOCKCOLOR$ = MID$(PIT$, ((10 * PITY) + PITX) + 1, 1)
1350 LINE ((PITX * 9) + 117, (PITY * 9) + 32)-STEP(9, 9), VAL("&H" + BLOCKCOLOR$), BF
1360 LINE ((PITX * 9) + 118, (PITY * 9) + 33)-STEP(6, 6), 0, B
1370 RETURN
1380 FOR BLOCKX = 0 TO 3
1390 FOR BLOCKY = 0 TO 3
1400 PITX = SHAPEX + BLOCKX: PITY = SHAPEY + BLOCKY
1410 IF PITX >= 0 AND PITX < 10 AND PITY >= 0 AND PITY < 16 THEN LINE ((PITX * 9) + 117, (PITY * 9) + 32)-STEP(9, 9), VAL("&H" + MID$(PIT$, ((10 * PITY) + PITX) + 1, 1)), BF
1420 NEXT BLOCKY: NEXT BLOCKX
1430 RETURN
